# Copyright 2010-2021, Google Inc.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
#     * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above
# copyright notice, this list of conditions and the following disclaimer
# in the documentation and/or other materials provided with the
# distribution.
#     * Neither the name of Google Inc. nor the names of its
# contributors may be used to endorse or promote products derived from
# this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

# Build rules for the Windows installer.

load("//:build_defs.bzl", "MOZC_TAGS", "mozc_py_binary")
load("//:config.bzl", "BRANDING")
load(":transition.bzl", "mozc_win_build_target")

package(default_visibility = ["//visibility:private"])

_TARGET_COMPATIBLE_WITH = [
    "@platforms//os:windows",
]

mozc_win_build_target(
    name = "mozc_tool",
    cpu = "x64_windows",
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//gui/tool:mozc_tool",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_win_build_target(
    name = "mozc_renderer",
    cpu = "x64_windows",
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//renderer/win32:win32_renderer_main",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_win_build_target(
    name = "mozc_server",
    cpu = "x64_windows",
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//server:mozc_server",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_win_build_target(
    name = "mozc_broker",
    cpu = "x64_windows",
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//win32/broker:mozc_broker_main",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_win_build_target(
    name = "mozc_cache_service",
    cpu = "x64_windows",
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//win32/cache_service:mozc_cache_service",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_win_build_target(
    name = "mozc_tip32",
    cpu = "x64_x86_windows",
    static_crt = True,
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//win32/tip:mozc_tip",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_win_build_target(
    name = "mozc_tip64",
    cpu = "x64_windows",
    static_crt = True,
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//win32/tip:mozc_tip",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_win_build_target(
    name = "custom_action",
    cpu = "x64_windows",
    static_crt = True,
    tags = MOZC_TAGS.WIN_ONLY,
    target = "//win32/custom_action",
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
)

mozc_py_binary(
    name = "build_installer",
    srcs = ["build_installer.py"],
    test_lib = False,
    deps = [
        "//build_tools:mozc_version_lib",
        "//build_tools:vs_util",
    ],
)

exports_files([
    "installer_64bit.wxs",
    "installer_oss_64bit.wxs",
])

config_setting(
    name = "debug_build",
    values = {
        "compilation_mode": "dbg",
    },
    visibility = ["//visibility:private"],
)

_WXS_FILE = ":installer_oss_64bit.wxs" if BRANDING == "Mozc" else ":installer_64bit.wxs"

_MSI_FILE = "Mozc64.msi" if BRANDING == "Mozc" else "GoogleJapaneseInput64.msi"

genrule(
    name = "installer",
    srcs = [
        ":mozc_tool",
        ":mozc_renderer",
        ":mozc_server",
        ":mozc_broker",
        ":mozc_cache_service",
        ":mozc_tip32",
        ":mozc_tip64",
        ":custom_action",
        _WXS_FILE,
        "//base:mozc_version_txt",
        "//data/images/win:product_icon.ico",
        "//data/installer:credits_en.html",
        "@qt_win//:bin/Qt6Core.dll",
        "@wix//:wix.exe",
    ],
    outs = [_MSI_FILE],
    cmd = " ".join([
        "$(location :build_installer)",
        "--output=$@",
        "--version_file=$(location //base:mozc_version_txt)",
        "--mozc_tool=$(location :mozc_tool)",
        "--mozc_renderer=$(location :mozc_renderer)",
        "--mozc_server=$(location :mozc_server)",
        "--mozc_broker=$(location :mozc_broker)",
        "--mozc_cache_service=$(location :mozc_cache_service)",
        "--mozc_tip32=$(location :mozc_tip32)",
        "--mozc_tip64=$(location :mozc_tip64)",
        "--custom_action=$(location :custom_action)",
        "--icon_path=$(location //data/images/win:product_icon.ico)",
        "--credit_file=$(location //data/installer:credits_en.html)",
        "--qt_core_dll=$(location @qt_win//:bin/Qt6Core.dll)",
        "--wxs_path=$(location " + _WXS_FILE + ")",
        "--wix_path=$(location @wix//:wix.exe)",
        "--branding=" + BRANDING,
    ]) + select({
        ":debug_build": " --debug_build",
        "//conditions:default": "",
    }),
    target_compatible_with = _TARGET_COMPATIBLE_WITH,
    tools = [
        ":build_installer",
    ],
    visibility = ["//:__subpackages__"],
)
